apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'java-library'

repositories {
    maven { url "https://hub.spigotmc.org/nexus/content/groups/public" }
    maven { url "https://repo.codemc.org/repository/maven-public" }
    maven { url "https://papermc.io/repo/repository/maven-public/" }
}

configurations.all { Configuration it ->
    it.resolutionStrategy { ResolutionStrategy rs ->
        rs.force("com.google.guava:guava:21.0")
    }
}

dependencies {
    api project(':worldedit-core')
    api project(':worldedit-libs:bukkit')
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'com.destroystokyo.paper:paper-api:1.13.2-R0.1-SNAPSHOT'
    implementation 'io.papermc:paperlib:1.0.2'
    compileOnly 'com.sk89q:dummypermscompat:1.10'
    compileOnly 'org.spigotmc:spigot:1.13.2-R0.1-SNAPSHOT'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.8.1'
    testCompile 'org.mockito:mockito-core:1.9.0-rc1'
    compileOnly 'com.massivecraft:factions:2.8.0'
    compileOnly 'com.drtshock:factions:1.6.9.5'
    compileOnly 'com.factionsone:FactionsOne:1.2.2'
    compileOnly 'me.ryanhamshire:GriefPrevention:11.5.2'
    compileOnly 'com.massivecraft:mcore:7.0.1'
    compileOnly 'net.sacredlabyrinth.Phaed:PreciousStones:10.0.4-SNAPSHOT'
    compileOnly 'net.jzx7:regios:5.9.9'
    compileOnly 'com.bekvon.bukkit.residence:Residence:4.5._13.1'
    compileOnly 'com.palmergames.bukkit:towny:0.84.0.9'
    compileOnly 'com.thevoxelbox.voxelsniper:voxelsniper:5.171.0'
    compileOnly 'com.comphenix.protocol:ProtocolLib-API:4.4.0-SNAPSHOT'
    compileOnly 'com.wasteofplastic:askyblock:3.0.8.2'
}

processResources {
    from('src/main/resources') {
        expand(
                name: project.parent.name,
                version: project.parent.version
        )
        include 'plugin.yml'
    }
//    from (zipTree('src/main/resources/worldedit-adapters.jar').matching {
//        exclude 'META-INF/'
//    })
//    exclude '**/worldedit-adapters.jar'
}

jar.archiveName="fawe-bukkit-${project.parent.version}.jar"
jar.destinationDir = file '../mvn/com/boydti/fawe-bukkit/' + project.parent.version
task createPom {
    doLast {
        pom {
            project {
                groupId 'com.boydti'
                artifactId 'fawe-bukkit'
                version project.parent.version
            }
        }
        .getEffectivePom()
                .setDependencies(new ArrayList<>())
                .writeTo("../mvn/com/boydti/fawe-bukkit/${project.parent.version}/fawe-bukkit-${project.parent.version}.pom")
        pom {
            project {
                groupId 'com.boydti'
                artifactId 'fawe-bukkit'
                version 'latest'
            }
        }
        .getEffectivePom()
                .setDependencies(new ArrayList<>())
                .writeTo("../mvn/com/boydti/fawe-bukkit/latest/fawe-bukkit-latest.pom")
    }
}
task copyFiles {
    doLast {
        copy {
            from "../mvn/com/boydti/fawe-bukkit/${project.parent.version}/"
            into '../mvn/com/boydti/fawe-bukkit/latest/'
            include('*.jar')
            rename ("fawe-bukkit-${project.parent.version}.jar", 'fawe-bukkit-latest.jar')
        }
    }
}
build.finalizedBy(copyFiles)
copyFiles.dependsOn(createPom)
